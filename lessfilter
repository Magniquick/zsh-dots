#!/usr/bin/env zsh

has_cmd() {
  for opt in "$@"; do
    (( $+commands[$opt] )) || return 1
  done
  return 0
}

mime=$(file -Lbs --mime-type "$1")
category=${mime%%/*}
kind=${mime##*/}
ext=${1##*.}

kitty-show(){
	kitten icat --clear --scale-up --unicode-placeholder --stdin=no --transfer-mode=memory --place="${FZF_PREVIEW_COLUMNS}"x"${FZF_PREVIEW_LINES}"@0x0 "$@"
}

if [ "$kind" = json ]; then
	if has_cmd jupyter bat && [ "$ext" = ipynb ]; then
		jupyter nbconvert --to python --stdout "$1" | bat --color=always -plpython
	elif has_cmd jq; then
		jq -Cr . "$1"
	fi
elif [ "$kind" = vnd.sqlite3 ]; then
	if has_cmd yes sqlite3 bat; then
		yes .q | sqlite3 "$1" | bat --color=always -plsql
	fi
# https://github.com/wofr06/lesspipe/pull/107
elif [ -d "$1" ]; then
	if has_cmd eza; then
		eza --no-user -hl --git --color=always --icons "$1"
	elif has_cmd exa; then
		exa -hl --color=always --icons "$1"
	fi
# https://github.com/wofr06/lesspipe/pull/110
elif [ "$kind" = pdf ]; then
	if has_cmd pdftoppm kitty; then
		pdftoppm -png -f 1 -singlefile "$1" /tmp/zsh_less_pdf 2> /dev/null
		kitty-show "/tmp/zsh_less_pdf.png"
	elif has_cmd pdftotext sed; then
		pdftotext -q "$1" - | sed "s/\f/$(printf '%*s' "${FZF_PREVIEW_COLUMNS:-80}" '' | tr ' ' 'â”€')\\n/g"
	fi
# https://github.com/wofr06/lesspipe/pull/115
elif [ "$kind" = rfc822 ]; then
	if has_cmd bat; then
		bat --color=always -lEmail "$1"
	fi
# https://github.com/wofr06/lesspipe/pull/106
elif [ "$category" = image ]; then
	if has_cmd kitty; then
		kitty-show "$1"
	elif [[ -e $HOME/.iterm2/imgcat ]]; then
		"$HOME/.iterm2/imgcat" -H 100% -W 100% "$1"
	elif has_cmd chafa; then
		chafa -f symbols "$1"
	elif has_cmd exiftool; then
		exiftool "$1" | bat --color=always -plyaml
	fi
# https://github.com/wofr06/lesspipe/pull/117
elif [ "$category" = text ]; then
	if has_cmd bat; then
		bat --color=always "$1"
	elif has_cmd pygmentize; then
		pygmentize "$1" | less
	fi
else
	echo "$category"
fi
